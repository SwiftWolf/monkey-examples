plugins {
    id 'java'
    id 'application'
}

group 'org.monkey.example'
version '1.0-SNAPSHOT'

ext.jmonkeyengineVersion = '3.5.2-stable'

tasks.withType(JavaCompile) { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true // to provide detailed deprecation warnings
    options.encoding = 'UTF-8'
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
Boolean isMacOS = DefaultNativePlatform.currentOperatingSystem.isMacOsX()

tasks.withType(JavaExec) { // Java runtime options:
    if (isMacOS) {
        jvmArgs '-XstartOnFirstThread'
    }
    classpath sourceSets.main.runtimeClasspath
    enableAssertions true
    //jvmArgs '-verbose:gc'
    //jvmArgs '-Xbatch'
    //jvmArgs '-Xms256m', '-Xmx256m'
    //jvmArgs '-XX:+PrintCompilation'
    jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
}

// NetBeans will automatically add "run" and "debug" tasks relying on the
// "mainClass" property. You may however define the property prior executing
// tasks by passing a "-PmainClass=<QUALIFIED_CLASS_NAME>" argument.
//
// Note however, that you may define your own "run" and "debug" tasks if you
// prefer. In this case NetBeans will not add these tasks but you may rely on
// your own implementation.

mainClassName = 'org.monkey.examples.MonkeyBuilder'
if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}
jar.manifest.attributes('Main-Class': mainClassName)

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation files('lib/monkey-v1.6.0/monkey-core-v1.5.1/monkey-core-v1.5.1.jar')
    implementation files('lib/monkey-v1.6.0/monkey-lemur-v1.0.0/monkey-lemur-v1.0.0.jar')

    implementation 'org.jmonkeyengine:jme3-core:' + jmonkeyengineVersion
    runtimeOnly 'org.jmonkeyengine:jme3-jogg:' + jmonkeyengineVersion
    runtimeOnly 'org.jmonkeyengine:jme3-plugins:' + jmonkeyengineVersion

    implementation 'org.jmonkeyengine:jme3-terrain:' + jmonkeyengineVersion
    implementation 'org.jmonkeyengine:jme3-effects:' + jmonkeyengineVersion
    implementation 'org.jmonkeyengine:jme3-jbullet:' + jmonkeyengineVersion
    implementation 'org.jmonkeyengine:jme3-networking:' + jmonkeyengineVersion

    // libraries related to the Lemur GUI and Groovy:
    implementation 'com.simsilica:lemur:1.16.0'
    implementation 'com.simsilica:lemur-props:1.2.0'
    implementation 'com.simsilica:lemur-proto:1.13.0'
    runtimeOnly 'org.codehaus.groovy:groovy-jsr223:3.0.10'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

java {
    withJavadocJar()
    //withSourcesJar()
}

test {
    useJUnitPlatform()
}